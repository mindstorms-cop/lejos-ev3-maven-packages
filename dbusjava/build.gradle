apply plugin: "maven-publish"

// Empty build task
task build

configurations {
    artifact
}

dependencies {
    artifact "net.java.dev.jna:jna:3.2.7"
}

task defaultArtifact(type: Jar, dependsOn: ":untarLejos") {
    baseName "artifact"
    destinationDir buildDir
    from zipTree("${lejosDownloadDir}/leJOS_EV3_0.9.0-beta/lib/ev3/dbusjava.jar")
    outputs.upToDateWhen { false }
}

task sourcesArtifact(type: Jar, dependsOn: ":untarLejos") {
    classifier "sources"
    baseName "artifact"
    destinationDir buildDir
    from zipTree("${lejosDownloadDir}/leJOS_EV3_0.9.0-beta/lib/ev3/dbusjava-src.zip")
    outputs.upToDateWhen { false }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "lejos-ev3-maven-packages"
            artifactId "dbusjava"
            version "0.9.0-beta"

            artifact defaultArtifact
            artifact sourcesArtifact

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.artifact.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

            }
        }
    }
}